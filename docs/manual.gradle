plugins {
  id "base"
  id "org.asciidoctor.convert" version "1.5.3"
}

group "daggerok"
version "0.4.1"

asciidoctorj {
  version = "1.5.5"
}

asciidoctor {
  sourceDir = project.file("src")
  sources {
    include "index.adoc"
  }
  outputDir = project.file("$buildDir/asciidoc")
  attributes toc: "left",
      idprefix: "",
      docinfo1: "",
      icons: "font",
      idseparator: "-",
      setanchors: "true",
      "source-highlighter": "coderay",
      "toc-title": "Table of Contents",
      "build-gradle": file("build.gradle"),
      "endpoint-url": "https://daggerok.github.io/spring-examples"
}

build.dependsOn asciidoctor
asciidoctor.shouldRunAfter clean

task wrapper(type: Wrapper) {
  gradleVersion = "4.5"
  distributionType = "ALL"
}

task deploy(type: Copy) {
  from "$buildDir/asciidoc/html5"
  from "$projectDir/src/static"
  into "$rootProject.projectDir/docs"
  dependsOn asciidoctor
  shouldRunAfter clean, asciidoctor, build
}

task docs(dependsOn: deploy)

clean.doFirst {
  delete "$rootProject.projectDir/docs"
}
